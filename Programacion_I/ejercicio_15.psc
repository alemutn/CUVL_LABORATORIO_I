Proceso ejercicio_15
	// Se realiza una inspecciÃ³n en una fÃ¡brica de pinturas, y se detectaron 20 infracciones. De cada infracciÃ³n se tomÃ³ nota de los siguientes datos:
	// - Tipo de InfracciÃ³n (1, 2, 3, Ã³ 4)
	// - Motivo de la infracciÃ³n
	// - Valor de la multa
	// - Gravedad de la infracciÃ³n (â€˜Lâ€™,â€˜Mâ€™, â€˜Gâ€™)
	// Se pide informar al final del proceso:
	// - Los valores totales de la multa a pagar de acuerdo al tipo de gravedad.
	// - La leyenda â€œClausurar fÃ¡bricaâ€ si la cantidad de infracciones 3 y 4 con gravedad â€œGâ€ sean mayor a 3.
	// ############## AnÃ¡lisis #############
	// Entradas
	// - tipoDeInfraccion  - Entero - Precondiciones: tipoDeInfraccion â‰¥ 1 Y tipoDeInfraccion â‰¤ 4
	// - motivoDeInfraccion - Cadena - Preciondiciones: - 
	// - valorMulta - Real - Preciondiciones. valorMulta > 0
	// - gravedadInfraccion - Caracter  - (â€˜Lâ€™,â€˜Mâ€™, â€˜Gâ€™)
	// - CANTIDAD_DE_INFRACCIONES
	// tipo de dato: Entero
	// condiciÃ³n: constante
	// valor: 20
	// Salidas
	// mensaje - variable - Cadena
	// totalLeves - variable - Real
	// totalModeradas - variable - Real
	// totalGraves - variable - Real
	Definir tipoInfraccion como Entero;
	Definir motivo como Cadena;
	Definir valorMulta como Real;
	Definir gravedadInfraccion como Caracter;
	Definir totalLeves como Real;
	Definir totalModeradas como Real;
	Definir totalGraves como Real;
	Definir CANTIDAD_DE_INFRACCIONES como Entero;
	CANTIDAD_DE_INFRACCIONES = 20;
	totalModeradas = 0;
	totalLeves = 0;
	totalGraves = 0;
	Definir i como Entero;
	Definir contadorClausura como Entero;
	contadorClausura = 0;
	// for (int i = 0; i < CANTIDAD_DE_INFRACCIONES ; i++) {...}
	Para i<-0 Hasta CANTIDAD_DE_INFRACCIONES - 1 Con Paso 1 Hacer
		Escribir "Ingrese tipo de infracción";
		Leer tipoInfraccion;
		Escribir "Ingrese motivo de la multa";
		Leer motivo;
		Escribir "Ingrese gravedad de la multa";
		Leer gravedadInfraccion;
		Escribir "Ingrese valor de la multa";
		Leer valorMulta;
		Segun gravedadInfraccion Hacer
			'L':
				totalLeves = totalLeves + valorMulta;
			'M':
				totalModeradas = totalModeradas + valorMulta;
			'G':
				totalGraves = totalGraves + valorMulta;
				Si tipoInfraccion == 3 o tipoInfraccion == 4 Entonces
					contadorClausura = contadorClausura + 1;
				FinSi
		FinSegun
	FinPara
	Escribir "El total de multas leves es: ", totalLeves;
	Escribir "El total de multas moderadas es: ", totalModeradas;
	Escribir "El total de multas graves es: ", totalGraves;
	Si contadorClausura > 3 Entonces
		Escribir "Clausurar fábrica";
	FinSi
FinProceso
